PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX ut: <http://example.org/utils#>
PREFIX fh: <http://hl7.org/fhir/>
PREFIX zk: <http://example.org/zika#>

CONSTRUCT {
    ?p zk:recentSexualEncounterWithZikaResidentOrTraveler true

} WHERE {
    ?p2 a fh:Patient ;
        fh:id/fh:v ?id2 .

    # recent travel to zika area
    {
        # http://build.fh.org/ig/HL7/case-reporting/StructureDefinition-us-ph-travel-history.html
        ?o2 a fh:Observation .
        ?o2 fh:subject/fh:reference/fh:v ?id2 .
        ?o2 fh:code/fh:coding/rdf:rest*/rdf:first/fh:code/fh:v 420008001 . # (SNOMED) Travel
    
        ?o2 fh:component/rdf:rest*/rdf:first ?cmp2 .
        ?cmp2 fh:code/fh:coding/rdf:rest*/rdf:first/fh:code/fh:v "LOC" .
        ?cmp2 fh:extension/rdf:rest*/rdf:first/fh:value/fh:country/fh:v ?country2 .
        FILTER (?country2 in ( 'American Samoa', 'Angola', 'Anguilla', 'Antigua and Barbuda', 'Argentina', 'Aruba', 'Bahamas', 'Bangladesh', 'Barbados', 'Belize', 'Bolivia', 'Bonaire', 'Brazil', 'British Virgin Islands', 'Burkina Faso', 'Burma', 'Burundi', 'Cambodia', 'Cameroon', 'Cape Verde', 'Cayman Islands', 'Central African Republic', 'Colombia', 'Cook Islands', 'Costa Rica', 'Cuba', 'Curacao', 'Dominica', 'Dominican Republic', 'Easter Island', 'Ecuador', 'El Salvador', 'Ethiopia', 'Federated States of Micronesia', 'Fiji', 'France', 'French Guiana', 'French Polynesia', 'Gabon', 'Grenada', 'Guadeloupe', 'Guatemala', 'Guinea-Bissau', 'Guyana', 'Haiti', 'Honduras', 'India', 'Indonesia', 'Ivory Coast', 'Jamaica', 'Kenya', 'Kiribati', 'Laos', 'Malaysia', 'Maldives', 'Marshall Islands', 'Martinique', 'Mexico', 'Montserrat', 'New Caledonia', 'Nicaragua', 'Nigeria', 'Palau', 'Panama', 'Papua New Guinea', 'Paraguay', 'Peru', 'Philippines', 'Puerto Rico', 'Saba', 'Saint Barthelemy', 'Saint Kitts and Nevis', 'Saint Lucia', 'Saint Martin', 'Saint Vincent and the Grenadines', 'Samoa', 'Senegal', 'Singapore', 'Sint Eustatius', 'Sint Maarten', 'Solomon Islands', 'Suriname', 'Thailand', 'Tonga', 'Trinidad and Tobago', 'Turks and Caicos', 'Uganda', 'United States (Continental US)', 'United States Virgin Islands', 'Vanuatu', 'Venezuela', 'Vietnam' ) ) .

        ?o2 fh:effective ?period2 . 
        ?period2 fh:start/fh:v ?start2 ; fh:end/fh:v ?end2 . 
        ut:now rdf:value ?now .
        BIND ((?now - ?end2) AS ?timeSince2) .

        FILTER (?timeSince2 < 2678400000) # 1 month
    }
    UNION
    # residentOfZikaArea
    {
        ?p2 fh:address/fh:country/fh:v ?country2 .
        FILTER (?country2 in ( 'American Samoa', 'Angola', 'Anguilla', 'Antigua and Barbuda', 'Argentina', 'Aruba', 'Bahamas', 'Bangladesh', 'Barbados', 'Belize', 'Bolivia', 'Bonaire', 'Brazil', 'British Virgin Islands', 'Burkina Faso', 'Burma', 'Burundi', 'Cambodia', 'Cameroon', 'Cape Verde', 'Cayman Islands', 'Central African Republic', 'Colombia', 'Cook Islands', 'Costa Rica', 'Cuba', 'Curacao', 'Dominica', 'Dominican Republic', 'Easter Island', 'Ecuador', 'El Salvador', 'Ethiopia', 'Federated States of Micronesia', 'Fiji', 'France', 'French Guiana', 'French Polynesia', 'Gabon', 'Grenada', 'Guadeloupe', 'Guatemala', 'Guinea-Bissau', 'Guyana', 'Haiti', 'Honduras', 'India', 'Indonesia', 'Ivory Coast', 'Jamaica', 'Kenya', 'Kiribati', 'Laos', 'Malaysia', 'Maldives', 'Marshall Islands', 'Martinique', 'Mexico', 'Montserrat', 'New Caledonia', 'Nicaragua', 'Nigeria', 'Palau', 'Panama', 'Papua New Guinea', 'Paraguay', 'Peru', 'Philippines', 'Puerto Rico', 'Saba', 'Saint Barthelemy', 'Saint Kitts and Nevis', 'Saint Lucia', 'Saint Martin', 'Saint Vincent and the Grenadines', 'Samoa', 'Senegal', 'Singapore', 'Sint Eustatius', 'Sint Maarten', 'Solomon Islands', 'Suriname', 'Thailand', 'Tonga', 'Trinidad and Tobago', 'Turks and Caicos', 'Uganda', 'United States (Continental US)', 'United States Virgin Islands', 'Vanuatu', 'Venezuela', 'Vietnam' ) ) .
    }

    ?p a fh:Patient .
    ?p fh:id/fh:v ?id .
    ?o a fh:Observation .
    ?o fh:subject/fh:reference/fh:v ?id .

    ?o fh:code/fh:coding/rdf:rest*/rdf:first/fh:code/fh:v 225517006 . # (SNOMED) Sexual partners
    ?o fh:value/fh:reference/fh:v ?id2 .

    ?o fh:effective ?period . 
    ?period fh:start/fh:v ?start ; fh:end/fh:v ?end . 
    ut:now rdf:value ?now .
    BIND ((?now - ?end) AS ?timeSince) .
    FILTER (?timeSince < 2678400000) # 1 month
}